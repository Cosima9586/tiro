apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'checkstyle'

mainClassName = "tiro.TIROStaticAnalysis"
sourceCompatibility = 1.8

jar {
    baseName = 'TIROStaticAnalysis'
    version = '0.2.0'
    manifest { attributes 'Main-Class': mainClassName }
}

sourceSets {
    main {
        java {
            srcDir 'src/'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    compile group: 'org.slf4j', name: 'slf4j-nop', version: '1.7.21'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    compile group: 'com.google.guava', name: 'guava', version: '20.0'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
}

compileJava {
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
}

checkstyle {
    toolVersion '7.8.1'
    configFile file("checkstyle.xml")
}

checkstyleMain.exclude 'soot/**'

distZip {
    into(project.name + '/libs') {
        from './libs'
    }
}

task fatJar(type: Jar) {
    baseName = 'TIROStaticAnalysis-all'
    manifest { attributes 'Main-Class': mainClassName }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

//artifacts {
//    archives fatJar
//}

run {
    if (project.hasProperty("appArgs")) {
        if (appArgs.contains("[")) {
            args Eval.me(appArgs)
        } else {
            args appArgs.split()
        }
    }

    jvmArgs "-Xmx8g", "-Xms6g", "-XX:NewSize=4g", "-XX:NewRatio=2"
}

processTestResources.enabled = false
compileTestJava.enabled = false
checkstyleTest.enabled = false
testClasses.enabled = false
test.enabled = false

task wrapper(type: Wrapper) {
    gradleVersion = '3.2.1'
}

